{
  lib,
  fetchzip,
  stdenvNoCC,
  steamDisplayName ? "DW-Proton",
}: let
  inherit (lib.licenses) bsd3;
  inherit (lib.sourceTypes) binaryNativeCode;
  inherit (lib.strings) fromJSON readFile;
  inherit (stdenvNoCC) mkDerivation;

  source = fromJSON <| readFile ./source.json;
in
  mkDerivation (finalAttrs: {
    pname = "dw-proton-bin";
    inherit (source) version;

    src = fetchzip {inherit (source) url hash;};

    outputs = ["out" "steamcompattool"];

    dontUnpack = true;
    dontConfigure = true;
    dontBuild = true;

    installPhase = ''
      runHook preInstall

      echo "${finalAttrs.pname} should not be installed into environments. Please use programs.steam.extraCompatPackages instead." > $out

      mkdir $steamcompattool
      ln -s $src/* $steamcompattool
      rm $steamcompattool/compatibilitytool.vdf
      cp $src/compatibilitytool.vdf $steamcompattool

      runHook postInstall
    '';

    preFixup = ''
      substituteInPlace "$steamcompattool/compatibilitytool.vdf" \
        --replace-fail "dwproton-${finalAttrs.version}-x86_64" "${steamDisplayName}"
    '';

    meta = {
      description = "Dawn Winery's custom Proton fork with fixes for various games :xdd:";
      homepage = "https://dawn.wine/dawn-winery/dwproton";
      license = bsd3;
      platforms = ["x86_64-linux"];
      sourceProvenance = [binaryNativeCode];
    };
  })
